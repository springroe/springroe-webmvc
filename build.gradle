plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'  // 组件签名插件
    id "org.sonarqube" version "3.3"
}

sonarqube {
    properties {
        property "sonar.projectKey", "springroe_springroe-webmvc"
        property "sonar.organization", "springroe"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

apply from: "$rootDir/gradle/dependencies.gradle"

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    }
}


subprojects {

    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    group 'com.springroe.webmvc'
    version = springroeVersion

    dependencies {

        api "io.github.springroe:springroe-data-core:$springroeVersion"

        // https://mvnrepository.com/artifact/org.springframework/spring-tx
        implementation "org.springframework:spring-tx:${springVersion}"
        // https://mvnrepository.com/artifact/org.springframework/spring-aop
        implementation "org.springframework:spring-aop:${springVersion}"
        // https://mvnrepository.com/artifact/org.springframework/spring-aop
        implementation "org.springframework:spring-webmvc:${springVersion}"

        implementation "io.swagger.core.v3:swagger-annotations:$swaggerVersion"

        // https://mvnrepository.com/artifact/com.querydsl/querydsl-core
        implementation "com.querydsl:querydsl-core:$querydslVersion"

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"

        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.deprecation = true
        options.compilerArgs += ["-Xlint:unchecked", "-Xlint:deprecation", "-parameters"]
    }

    apply from: "$rootDir/gradle/publishing.gradle"

}


allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    }
}